
buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.5'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'org.flywaydb.flyway' version '6.0.0-beta' //'5.2.4'
    id 'jacoco'
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.3"
//    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

flyway {
    def props = new Properties()
    file("application.properties").withInputStream { props.load(it) }
    url = props.getProperty("spring.datasource.url") // 'jdbc:postgresql://localhost:5555/app'
    user = props.getProperty("spring.datasource.username") //'postgres'
    password = props.getProperty("spring.datasource.password") //'postgres'
}


group 'org.thinkbigthings'
version '1.0-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {

    implementation 'javax.inject:javax.inject:1'
    implementation 'org.postgresql:postgresql:42.2.5'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.1.4.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.1.4.RELEASE'
    
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.27.0'
}

test {
    useJUnitPlatform()
}


task cleanRun(type: GradleBuild) {
    tasks = ['clean', 'flywayClean', 'flywayMigrate', 'bootRun']
}